buildscript {
    repositories {
        maven {
            url projectRepositoriesMavenUrl
        }
    }
}

plugins {
    id 'jacoco'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.gradle.versions.plugin)
    alias(libs.plugins.grgit.gradle)
    alias(libs.plugins.license.gradle.plugin)
    alias(libs.plugins.lombok.plugin)
    alias(libs.plugins.spotless.plugin.gradle)
    alias(libs.plugins.version.catalog.update)
}

group = "$groupPackage" as Object
version = "$projectVersion" as Object
description = "$rootProject.name"

// import gradle files
// List of Gradle files to apply
List<String> gradleFiles = [
        "gradle/dependencies.gradle",
        "gradle/formatting.gradle",
        "gradle/grgit.gradle",
        "gradle/licensing.gradle",
        "gradle/packaging.gradle",
        "gradle/publishing.gradle",
        "gradle/repositories.gradle",
        "gradle/testing.gradle",
        "gradle/version-catalog-update.gradle"
]

// Apply the Gradle files
applyGradleFiles(gradleFiles)


java {
    sourceCompatibility = "$projectSourceCompatibility" as Object
    toolchain {
        languageVersion = JavaLanguageVersion.of("$projectSourceCompatibility")
    }
}

tasks.named("test") {
    it.mustRunAfter(tasks.named("jar"))
}

tasks.withType(JavaCompile).configureEach { javaCompiler ->
    options.encoding = "UTF-8"
    // uncomment the next line for build with compiler argument -Xlint:deprecation
    // options.compilerArgs << "-Xlint:deprecation"
    // uncomment the next line for build with compiler argument -Xlint:unchecked
    // options.compilerArgs << "-Xlint:unchecked"
}

void applyGradleFiles(List<String> gradleFiles) {
    gradleFiles.each { file ->
        apply from: file
    }
}
